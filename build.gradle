buildscript {
    repositories {
        // These repositories are only for Gradle plugins, put any other repositories in the repository block further below
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
        mavenCentral()
    }
    dependencies {
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}

plugins {
    id 'idea'
    id 'net.minecraftforge.gradle' version '[6.0.16,6.2)'
    id 'com.matthewprenger.cursegradle' version '1.4.0' // CurseForge uploader
    id "com.modrinth.minotaur" version "2.8.10" // Modrinth uploader
}

apply plugin: 'org.spongepowered.mixin'

group = mod_group_id
version = mod_version

base {
    archivesName = mod_id
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(17)
}

minecraft {
    // The mappings can be changed at any time and must be in the following format.
    // Channel:   Version:
    // official   MCVersion             Official field/method names from Mojang mapping files
    // parchment  YYYY.MM.DD-MCVersion  Open community-sourced parameter names and javadocs layered on top of official
    //
    // You must be aware of the Mojang license when using the 'official' or 'parchment' mappings.
    // See more information here: https://github.com/MinecraftForge/MCPConfig/blob/master/Mojang.md
    //
    // Parchment is an unofficial project maintained by ParchmentMC, separate from MinecraftForge
    // Additional setup is needed to use their mappings: https://parchmentmc.org/docs/getting-started
    //
    // Use non-default mappings at your own risk. They may not always work.
    // Simply re-run your setup task after changing the mappings to update your workspace.
    mappings channel: mapping_channel, version: mapping_version

    // When true, this property will have all Eclipse/IntelliJ IDEA run configurations run the "prepareX" task for the given run configuration before launching the game.
    // In most cases, it is not necessary to enable.
    // enableEclipsePrepareRuns = true
    // enableIdeaPrepareRuns = true

    // This property allows configuring Gradle's ProcessResources task(s) to run on IDE output locations before launching the game.
    // It is REQUIRED to be set to true for this template to function.
    // See https://docs.gradle.org/current/dsl/org.gradle.language.jvm.tasks.ProcessResources.html
    copyIdeResources = true

    // When true, this property will add the folder name of all declared run configurations to generated IDE run configurations.
    // The folder name can be set on a run configuration using the "folderName" property.
    // By default, the folder name of a run configuration is the name of the Gradle project containing it.
    // generateRunFolders = true

    // This property enables access transformers for use in development.
    // They will be applied to the Minecraft artifact.
    // The access transformer file can be anywhere in the project.
    // However, it must be at "META-INF/accesstransformer.cfg" in the final mod jar to be loaded by Forge.
    // This default location is a best practice to automatically put the file in the right place in the final jar.
    // See https://docs.minecraftforge.net/en/latest/advanced/accesstransformers/ for more information.
    // accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    // Default run configurations.
    // These can be tweaked, removed, or duplicated as needed.
    runs {
        // applies to all the run configs below
        configureEach {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            // The markers can be added/remove as needed separated by commas.
            // "SCAN": For mods scan.
            // "REGISTRIES": For firing of registry events.
            // "REGISTRYDUMP": For getting the contents of all registries.
            property 'forge.logging.markers', 'REGISTRIES'


            // Recommended logging level for the console
            // You can set various levels here.
            // Please read: https://stackoverflow.com/questions/2031163/when-to-use-the-different-log-levels
            property 'forge.logging.console.level', 'debug'

            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }

        client {
            // Comma-separated list of namespaces to load gametests from. Empty = all namespaces.
            property 'forge.enabledGameTestNamespaces', mod_id
            jvmArgs '-Diris.debug=true'
            jvmArg  '-Dmixin.env.disableRefMap=false'
            jvmArgs '-Dofsc.debug.blitClouds=false'
            jvmArgs '-Dofsc.debug.depthLog=true'

        }

        server {
            property 'forge.enabledGameTestNamespaces', mod_id
            args '--nogui'
        }

        // This run config launches GameTestServer and runs all registered gametests, then exits.
        // By default, the server will crash when no gametests are provided.
        // The gametest system is also enabled by default for other run configs under the /test command.
        gameTestServer {
            property 'forge.enabledGameTestNamespaces', mod_id
        }

        data {
            // example of overriding the workingDirectory set in configureEach above
            workingDirectory project.file('run-data')

            // Specify the modid for data generation, where to output the resulting resource, and where to look for existing resources.
            args '--mod', mod_id, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
        }
    }
}

mixin {
    add sourceSets.main, "${mod_id}.refmap.json"

    config "${mod_id}.mixins.json"
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    // Put repositories for dependencies here
    // ForgeGradle automatically adds the Forge maven and Maven Central for you
    maven {
        name = "CurseForge"
        url = "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven { url = "https://api.modrinth.com/maven" }
    maven {
        name "nonamecrackers2Maven"
        url "https://maven.nonamecrackers2.dev/releases"
    }
    // If you have mod jar dependencies in ./libs, you can declare them as a repository like so.
    // See https://docs.gradle.org/current/userguide/declaring_repositories.html#sub:flat_dir_resolver
    // flatDir {
    //     dir 'libs'
    // }
}

dependencies {
    // Specify the version of Minecraft to use.
    // Any artifact can be supplied so long as it has a "userdev" classifier artifact and is a compatible patcher artifact.
    // The "userdev" classifier will be requested and setup by ForgeGradle.
    // If the group id is "net.minecraft" and the artifact id is one of ["client", "server", "joined"],
    // then special handling is done to allow a setup of a vanilla dependency without the use of an external repository.
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    implementation fg.deobf("curse.maven:oculus-581495:6020952")
    implementation fg.deobf("curse.maven:embeddium-908741:5681725")
    implementation fg.deobf("nonamecrackers2:simpleclouds:${simpleclouds_version}")
    implementation fg.deobf("nonamecrackers2:crackerslib-forge:${crackerslib_version}")
    implementation fg.deobf("nonamecrackers2:simplecloudsapi-forge:${api_version}")
    implementation fg.deobf("curse.maven:chloride-931925:6615986")
    compileOnly fg.deobf("curse.maven:distant-horizons-508933:7104610")
    runtimeOnly fg.deobf("curse.maven:alexs-caves-924854:5848216")
    runtimeOnly fg.deobf("curse.maven:distant-horizons-508933:7104610")
    compileOnly fg.deobf("curse.maven:oculus-for-simpleclouds-1386686:7258925")
    compileOnly fg.deobf("curse.maven:immersive-engineering-231951:6206989")
    runtimeOnly fg.deobf("curse.maven:immersive-engineering-231951:6206989")
    runtimeOnly fg.deobf("curse.maven:citadel-331936:6702068")

    // Example mod dependency with JEI - using fg.deobf() ensures the dependency is remapped to your development mappings
    // The JEI API is declared for compile time use, while the full JEI artifact is used at runtime
    // compileOnly fg.deobf("mezz.jei:jei-${mc_version}-common-api:${jei_version}")
    // compileOnly fg.deobf("mezz.jei:jei-${mc_version}-forge-api:${jei_version}")
    // runtimeOnly fg.deobf("mezz.jei:jei-${mc_version}-forge:${jei_version}")

    // Example mod dependency using a mod jar from ./libs with a flat dir repository
    // This maps to ./libs/coolmod-${mc_version}-${coolmod_version}.jar
    // The group id is ignored when searching -- in this case, it is "blank"
    // implementation fg.deobf("blank:coolmod-${mc_version}:${coolmod_version}")

    // For more info:
    // http://www.gradle.org/docs/current/userguide/artifact_dependencies_tutorial.html
    // http://www.gradle.org/docs/current/userguide/dependency_management.html

    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'

}

// This block of code expands all declared replace properties in the specified resource targets.
// A missing property will result in an error. Properties are expanded using ${} Groovy notation.
// When "copyIdeResources" is enabled, this will also run before the game launches in IDE environments.
// See https://docs.gradle.org/current/dsl/org.gradle.language.jvm.tasks.ProcessResources.html
tasks.named('processResources', ProcessResources).configure {
    var replaceProperties = [minecraft_version   : minecraft_version, minecraft_version_range: minecraft_version_range,
                             forge_version       : forge_version, forge_version_range: forge_version_range,
                             loader_version_range: loader_version_range,
                             mod_id              : mod_id, mod_name: mod_name, mod_license: mod_license, mod_version: mod_version,
                             mod_authors         : mod_authors, mod_description: mod_description,]

    inputs.properties replaceProperties

    filesMatching(['META-INF/mods.toml', 'pack.mcmeta']) {
        expand replaceProperties + [project: project]
    }
}

// Example for how to get properties into the manifest for reading at runtime.
tasks.named('jar', Jar).configure {
    manifest {
        attributes(["Specification-Title"     : mod_id,
                    "Specification-Vendor"    : mod_authors,
                    "Specification-Version"   : "1", // We are version 1 of ourselves
                    "Implementation-Title"    : project.name,
                    "Implementation-Version"  : project.jar.archiveVersion,
                    "Implementation-Vendor"   : mod_authors,
                    "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")])
    }

    // This is the preferred method to reobfuscate your jar file
    finalizedBy 'reobfJar'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}
def modrinthToken = System.getenv("MODRINTH_TOKEN")
def curseforgeToken = System.getenv("CURSEFORGE_TOKEN")

if (modrinthToken != null && !modrinthToken.isBlank()) {
    modrinth {
        token = modrinthToken
        projectId = "8WzyyTkv"
        versionNumber = "Forge-${project.version}"
        versionName = "Oculus for SimpleClouds ${mod_version}"
        changelog = rootProject.file("IRISSCchangelog.md").text
        gameVersions = ["1.20.1"]
        loaders = ["forge"]
        versionType = "beta"

        dependencies {
            required.project "GchcoXML"
            optional.project "qIWoLcKJ"
            required.project "LtZfKhel"
            required.project "N8aGZtvj"
        }

        uploadFile = file("build/libs/${archivesBaseName}-${version}.jar")
    }
} else {
    logger.lifecycle("MODRINTH_TOKEN not set, Modrinth publishing disabled.")
}

if (curseforgeToken != null && !curseforgeToken.isBlank()) {
    curseforge {
        apiKey = curseforgeToken

        project {
            id = "1386686"
            changelog = rootProject.file("IRISSCchangelog.md")
            releaseType = "beta"
            addGameVersion "1.20.1"
            addGameVersion "Forge"

            mainArtifact(file("build/libs/${archivesBaseName}-${version}.jar")) {
                displayName = "Forge-${version}"
            }
        }
    }
} else {
    logger.lifecycle("CURSEFORGE_TOKEN not set, CurseForge publishing disabled.")
}
import groovy.json.JsonOutput
import groovy.json.JsonOutput

tasks.register("discordWebhook") {
    onlyIf {
        def url = System.getenv("DISCORD_WEBHOOK_URL")
        return url != null && !url.isBlank()
    }

    doLast {
        def webhookUrl = System.getenv("DISCORD_WEBHOOK_URL")
        def changelogText = rootProject.file("IRISSCchangelog.md").text

        def payload = [
                username: "Build Bot",
                allowed_mentions: [parse: ["roles"]],
                content: "**New release published!** ðŸŽ‰\n" +
                        "<@&1440208473949208577>\n" +
                        "Project: ${mod_name}\n" +
                        "Version: v${project.mod_version}-1.20.1\n\n" +
                        "${changelogText.take(1891)}"
        ]

        println "Sending release notification to Discord..."
        def connection = new URL(webhookUrl).openConnection()
        connection.with {
            doOutput = true
            requestMethod = "POST"
            setRequestProperty("Content-Type", "application/json")
            outputStream.withWriter("UTF-8") { it << JsonOutput.toJson(payload) }
            println "Webhook sent with response code: ${responseCode}"
        }
    }
}

