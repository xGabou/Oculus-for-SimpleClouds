plugins {
    id 'java-library'
    id 'maven-publish'
    id 'idea'
    id 'net.neoforged.moddev' version '2.0.107'
    id 'com.matthewprenger.cursegradle' version '1.4.0' // CurseForge uploader
    id "com.modrinth.minotaur" version "2.8.10" // Modrinth uploader
}

group = mod_group_id
version = mod_version

base {
    archivesName = mod_id
}
repositories {
    maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
    maven { url "https://maven.blamejared.com/" }
    maven {
        url "https://www.cursemaven.com"
        content { includeGroup "curse.maven" }
    }
    maven {
        name = 'GeckoLib'
        url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/'
        content {
            includeGroupByRegex('software\\.bernie.*')
            includeGroup("com.eliotlash.mclib")
        }
    }
    maven { url = "https://api.modrinth.com/maven" }
    maven {
        name "nonamecrackers2Maven"
        url "https://maven.nonamecrackers2.dev/releases"
    }
    flatDir { dir 'libs' }
}


java.toolchain.languageVersion = JavaLanguageVersion.of(21)


neoForge {
    // Specify the version of NeoForge to use.
    version = project.neo_version

    parchment {
        mappingsVersion = project.parchment_mappings_version
        minecraftVersion = project.parchment_minecraft_version
    }

    // This line is optional. Access Transformers are automatically detected
    // accessTransformers.add('src/main/resources/META-INF/accesstransformer.cfg')

    // Default run configurations.
    // These can be tweaked, removed, or duplicated as needed.
    runs {
        client {
            client()

            // Comma-separated list of namespaces to load gametests from. Empty = all namespaces.
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        server {
            server()
            programArgument '--nogui'
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        // This run config launches GameTestServer and runs all registered gametests, then exits.
        // By default, the server will crash when no gametests are provided.
        // The gametest system is also enabled by default for other run configs under the /test command.
        gameTestServer {
            type = "gameTestServer"
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        data {
            data()

            // example of overriding the workingDirectory set in configureEach above, uncomment if you want to use it
            // gameDirectory = project.file('run-data')

            // Specify the modid for data generation, where to output the resulting resource, and where to look for existing resources.
            programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }

        // applies to all the run configs above
        configureEach {
            // Recommended logging data for a userdev environment
            // The markers can be added/remove as needed separated by commas.
            // "SCAN": For mods scan.
            // "REGISTRIES": For firing of registry events.
            // "REGISTRYDUMP": For getting the contents of all registries.
            systemProperty 'forge.logging.markers', 'REGISTRIES'

            // Recommended logging level for the console
            // You can set various levels here.
            // Please read: https://stackoverflow.com/questions/2031163/when-to-use-the-different-log-levels
            logLevel = org.slf4j.event.Level.DEBUG
        }
    }

    mods {
        // define mod <-> source bindings
        // these are used to tell the game which sources are for which mod
        // mostly optional in a single mod project
        // but multi mod projects should define one per mod
        "${mod_id}" {
            sourceSet(sourceSets.main)
        }
    }
}


sourceSets.main.resources { srcDir 'src/generated/resources' }
repositories {
    // Put repositories for dependencies here
    // ForgeGradle automatically adds the Forge maven and Maven Central for you
    maven {
        name = "CurseForge"
        url = "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven { url = "https://api.modrinth.com/maven" }
    maven {
        name "nonamecrackers2Maven"
        url "https://maven.nonamecrackers2.dev/releases"
    }
    // If you have mod jar dependencies in ./libs, you can declare them as a repository like so.
    // See https://docs.gradle.org/current/userguide/declaring_repositories.html#sub:flat_dir_resolver
    // flatDir {
    //     dir 'libs'
    // }
}

dependencies {
    // Specify the version of Minecraft to use.
    // Any artifact can be supplied so long as it has a "userdev" classifier artifact and is a compatible patcher artifact.
    // The "userdev" classifier will be requested and setup by ForgeGradle.
    // If the group id is "net.minecraft" and the artifact id is one of ["client", "server", "joined"],
    // then special handling is done to allow a setup of a vanilla dependency without the use of an external repository.
    implementation "curse.maven:irisshaders-455508:6661598"
    implementation "curse.maven:sodium-394468:6382651"
    implementation "curse.maven:simple-clouds-1121215:6928979"
    implementation "nonamecrackers2:crackerslib-neoforge:${crackerslib_version}"
    implementation "nonamecrackers2:simplecloudsapi-neoforge:${api_version}"
    implementation "curse.maven:chloride-931925:6616326"

    // Example mod dependency with JEI - using fg.deobf() ensures the dependency is remapped to your development mappings
    // The JEI API is declared for compile time use, while the full JEI artifact is used at runtime
    // compileOnly fg.deobf("mezz.jei:jei-${mc_version}-common-api:${jei_version}")
    // compileOnly fg.deobf("mezz.jei:jei-${mc_version}-forge-api:${jei_version}")
    // runtimeOnly fg.deobf("mezz.jei:jei-${mc_version}-forge:${jei_version}")

    // Example mod dependency using a mod jar from ./libs with a flat dir repository
    // This maps to ./libs/coolmod-${mc_version}-${coolmod_version}.jar
    // The group id is ignored when searching -- in this case, it is "blank"
    // implementation fg.deobf("blank:coolmod-${mc_version}:${coolmod_version}")

    // For more info:
    // http://www.gradle.org/docs/current/userguide/artifact_dependencies_tutorial.html
    // http://www.gradle.org/docs/current/userguide/dependency_management.html

    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'

}
var generateModMetadata = tasks.register("generateModMetadata", ProcessResources) {
    var replaceProperties = [minecraft_version      : minecraft_version,
                             minecraft_version_range: minecraft_version_range,
                             neo_version            : neo_version,
                             neo_version_range      : neo_version_range,
                             loader_version_range   : loader_version_range,
                             mod_id                 : mod_id,
                             mod_name               : mod_name,
                             mod_license            : mod_license,
                             mod_version            : mod_version,
                             mod_authors            : mod_authors,
                             mod_description        : mod_description]
    inputs.properties replaceProperties
    expand replaceProperties
    from "src/main/templates"
    into "build/generated/sources/modMetadata"
}

// Include the output of "generateModMetadata" as an input directory for the build
// this works with both building through Gradle and the IDE.
sourceSets.main.resources.srcDir generateModMetadata
// To avoid having to run "generateModMetadata" manually, make it run on every project reload
neoForge.ideSyncTask generateModMetadata

// Example configuration to allow publishing using the maven-publish plugin
publishing {
    publications {
        register('mavenJava', MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/repo"
        }
    }
}

// IDEA no longer automatically downloads sources/javadoc jars for dependencies, so we need to explicitly enable the behavior.
idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}
modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = "8WzyyTkv" // from your Modrinth project URL
    versionNumber = "NeoForge-${project.version}"
    versionName = "Project Atmosphere ${mod_version}"
    changelog = rootProject.file("IRISSCchangelog.md").text
    gameVersions = ["1.21.1"]
    loaders = ["neoforge"]
    versionType = "beta"
    dependencies {
        required.project "e0bNACJD"
        required.project "GchcoXML"
        optional.project "qIWoLcKJ"
    }
    uploadFile = file("build/libs/${archivesBaseName}-${version}.jar")
}
curseforge {
    apiKey = System.getenv("CURSEFORGE_TOKEN")

    project {
        id = "1386686"
        changelog = rootProject.file("IRISSCchangelog.md")
        releaseType = "beta" // options: alpha, beta, release
        addGameVersion "1.21.1"
        addGameVersion "NeoForge"
        mainArtifact(file("build/libs/${archivesBaseName}-${version}.jar")) {
            displayName = "NeoForge-${version}"
        }
    }
}
import groovy.json.JsonOutput
tasks.register("discordWebhook") {
    doLast {
        def webhookUrl = System.getenv("DISCORD_WEBHOOK_URL")
        def changelogText = rootProject.file("IRISSCchangelog.md").text

        def payload = [
                username: "Build Bot",
                allowed_mentions: [ parse: ["roles"] ],
                content: "**New release published!** ðŸŽ‰\n" +
                        "<@&1440208473949208577>\n" + // role mention
                        "Project: ${mod_name}\n" +
                        "Version: v${project.mod_version}-1.21.1\n\n" +
                        "${changelogText.take(1891)}"
        ]

        println "Sending release notification to Discord..."
        def connection = new URL(webhookUrl).openConnection()
        connection.with {
            doOutput = true
            requestMethod = "POST"
            setRequestProperty("Content-Type", "application/json")
            outputStream.withWriter("UTF-8") { it << JsonOutput.toJson(payload) }
            println "Webhook sent with response code: ${responseCode}"
        }
    }
}